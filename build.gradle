plugins {
    id "org.miscar.gradle-utilities" version "2021.1.1"
}

miscar {
    submodules = false
}

deploy {
    targets {
        roboRIO("robot") {
            team = frc.getTeamOrDefault(1574)
        }
    }
    artifacts {
        frcNativeArtifact("code") {
            targets << "robot"
            component = "program"
            debug = frc.getDebugOrDefault(false)
        }
        fileTreeArtifact("files") {
            targets << "robot"
            files = fileTree(dir: "src/main/deploy")
            directory = "/home/lvuser/deploy"
        }
    }
}

dependencies {
    simulation wpi.deps.sim.gui(wpi.platforms.desktop, true)
    simulation wpi.deps.sim.driverstation(wpi.platforms.desktop, true)
}

model {
    components {
        program(NativeExecutableSpec) {
            targetPlatform wpi.platforms.roborio
            targetPlatform wpi.platforms.desktop

            sources.cpp {
                source {
                    srcDir "src/main/cpp"
                    include "**/*.cpp", "**/*.cc"
                }
                exportedHeaders {
                    srcDir "src/main/cpp"
                }
            }

            wpi.deps.vendor.cpp(it)
            wpi.deps.wpilib(it)
        }
    }
    testSuites {
        test(GoogleTestTestSuiteSpec) {
            testing $.components.program

            sources.cpp {
                source {
                    srcDir "src/test/cpp"
                    include "**/*.cpp"
                }
            }

            wpi.deps.vendor.cpp(it)
            wpi.deps.wpilib(it)
            wpi.deps.googleTest(it)
        }
    }
}
